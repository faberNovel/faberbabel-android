import com.amazonaws.auth.AWSCredentials
import com.amazonaws.auth.profile.ProfileCredentialsProvider

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath "com.amazonaws:aws-java-sdk-core:1.11.534"
    }
}

project.ext {
    fetchAwsCredentials = {
        if (System.getenv('AWS_S3_ACCESS_KEY') != null &&
            System.getenv('AWS_S3_SECRET_KEY') != null) {
            return [
                awsAccessKey: System.getenv('AWS_S3_ACCESS_KEY'),
                awsSecretKey:  System.getenv('AWS_S3_SECRET_KEY')
            ]
        } else {
            try {
                return new ProfileCredentialsProvider("ft_maven_repo").credentials
            } catch (Exception e) {
                throw new GradleException(
                    'Unable to retrieve AWS credentials from environment variable or profile, publishing to S3 will not be available.\n' +
                        'Please retrieve and add the `ft_maven_repo` credentials to your local configuration `~/.aws/credentials`'
                )
            }
        }
    }
}